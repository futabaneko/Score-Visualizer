# -------------------------------------------
#   Music Player skript
#   author: @futabaneko (discord)
#   version: 2.0 (2025/02/19)
# -------------------------------------------
#  Arguments:
#    {music::playing::ID::% id %} player / Variable to save Processing ID
#    {music::playing::IDRev::% player %} id / Variable to save Processing ID
#
#    {music::volume::%id%} volume
#    {music::range::%id%} range (if 0 == player, 0 != block)
#    {music::ProcessID::*} String / list of ID of processing
#
#    {music::notes::%id%} note count
#    {music::ticks::%id%} ticks
#    {music::seconds::%id%} seconds
#    {music::start::%id%} start time
#
#    {music::que::% time %::*} String / que that is executed regularly (when the player is an player)
#
# -------------------------------------------
#  Commands used in input:
#    on skript: ~
#    on chat  : /music ~
#
#  ~ :
#    "% instruments %% pitch %% instrument %% pitch %% wait tick % ...."
#
#  instruments:
#    hat        : !
#    bell       : /
#    harp       : \
#    snare      : ?
#    chime      : _
#    xylophone  : ,
#    basedrum   : ^
#    flute      : @
#    pling      : *, " "
#    bass       : (
#    guitar     : )
#    exp        : ~
#
#  pitch:
#    A ~ Y
# 
# -------------------------------------------
#  Commands used in queues:
#    play-% id %-% type %-% pitch % : play the note sound
#    end-% id %                     : Stop playing music
# -------------------------------------------

options:
    maximum_timer: 12000


on load:
    set {music::timer} to 0 
    send "&7[ &b♪ &7] &emusic.sk&7 を再読み込みしました" to ops
    loop {music::ProcessID::*}:
        music_stop_from_id(loop-value)
    
    set {_cache} to music_clear_cache()
    if {_cache} is not 0:
        send "&7[ &b♪ &7] %{_cache}% &7件のキャッシュデータを削除しました" to ops
        
command /mp [<text>]:
    aliases: m
    trigger:
        set {_id} to music_start_play_sound_at_player(player, arg-1, 1, 0)
        send "&7[ &b♪ &7] id: &b%{_id}% &7の再生を開始しました (音符数: &e%{music::notes::%{_id}%}%&7, 推定再生時間: &a%{music::seconds::%{_id}%}%&7 秒)" to player

# コマンドで実行できる
command /mpatcommand [<player>] [<text>] [<number>]:
    aliases: mac
    permission: skript.music
    trigger:
        if arg-3 is not set:
            set {_range} to 0
        
        else:
            set {_range} to arg-3

        set {_id} to music_start_play_sound_at_player(arg-1, arg-2, 1, {_range})
        send "&7[ &b♪ &7] id: &b%{_id}% &7の再生を開始しました (音符数: &e%{music::notes::%{_id}%}%&7, 推定再生時間: &a%{music::seconds::%{_id}%}%&7 秒)" to arg-1

# ２つ上のコマンドブロックの中に格納された楽譜を <player> に対して再生するデバッグコマンド
command /mpeasy [<player>]:
    aliases: mpe
    permission: skript.music
    trigger:
        sender is not an player
        set {_loc} to location of event-block
        add 2 to y-coordinate of {_loc}

        if "%block at {_loc}%" contains "command":
            set {_score} to "%tag ""Command"" of full nbt of block at {_loc}%"

            set {_id} to music_start_play_sound_at_player(arg-1, {_score}, 1, 0)
            send "&7[ &b♪ &7] id: &b%{_id}% &7の再生を開始しました (音符数: &e%{music::notes::%{_id}%}%&7, 推定再生時間: &a%{music::seconds::%{_id}%}%&7 秒)" to arg-1
        
        else:
            send "&7[ &b♪ &7] コマンドブロックが発見できませんでした" to arg-1
            
# 周囲に聞こえる音楽を再生し、２つ上のブロックの上に、現在再生している音に対応する音符を表示するコマンド
command /mpwithparticle [<text>]:
    aliases: mwp
    permission: skript.music
    trigger:
        sender is not an player
        set {_loc} to location of event-block
        add 1 to y-coordinate of {_loc}
        set {_id} to music_start_play_sound_at_block({_loc}, arg-1, 1, 10)


# 音楽の再生を停止するコマンド
command /musicstop:
    aliases: ms
    trigger:
        if {music::playing::IDRev::%player%} is set:
            set {_id} to {music::playing::IDRev::%player%}
            music_stop_from_id({_id})

            send "&7[ &b♪ &7] 再生を停止しました" to player

# 現在再生中の音楽一覧を表示するデバッグコマンド
command /musiclist:
    aliases: ml
    permission: skript.music
    trigger:
        send "&7[ &b♪ &7] -----[ List of Playing Music ID ]-----" to player
        loop {music::ProcessID::*}:
            set {_id} to loop-value
            set {_volume} to {music::volume::%{_id}%}
            set {_range} to {music::range::%{_id}%}
            set {_center} to {music::playing::ID::%{_id}%}

            if {music::playing::ID::%{_id}%} is an player:
                set {_target} to "&bPLAYER (%{music::playing::ID::%{_id}%}%)"
            
            else:
                set {_target} to "&dBLOCK"
            
            send "&7[ &b♪ &7] &d%{_id}% &7/ %{_target}% &7/ volume: &a%{_volume}% &7/ range: &9%{_range}%" to player
            
        send "&7[ &b♪ &7] ------------------------------------" to player

# サーバー内で再生されているすべての音楽を停止するデバッグコマンド
command /musicreset:
    aliases: mr
    permission: skript.music
    trigger:
        send "&7[ &b♪ &7] 全ての再生中の音楽を停止しました" to player
        loop {music::ProcessID::*}:
            music_stop_from_id(loop-value)
        
        set {_cache} to music_clear_cache()
        if {_cache} is not 0:
            send "&7[ &b♪ &7] %{_cache}% &7件のキャッシュデータを削除しました" to player


# 毎tick実行
every ticks:
    set {music::timer} to mod({music::timer} + 1, {@maximum_timer})

    if 0 < number of {music::que::%{music::timer}%::*}:
        loop {music::que::%{music::timer}%::*}:
            set {_temp::*} to loop-value split at "-"
            set {_id} to {_temp::2}

            if {_temp::1} is "play":
                # broadcast "%{music::ProcessID::*}% %{_id}%"
                if {music::ProcessID::*} contains {_id}:
                    set {_inst} to music_get_instrument({_temp::3})
                    set {_pitch} to music_get_pitch({_temp::4})
                    set {_volume} to {music::volume::%{_id}%}
                    set {_range} to {music::range::%{_id}%}
                    set {_target} to {music::playing::ID::%{_id}%}

                    # on world
                    if {_range} is -1:
                        set {_world} to {music::world::%{_id}%}
                        loop all players:
                            if world of loop-player is {_world}:
                                play sound "%{_inst}%" with volume {_volume} with pitch {_pitch} at loop-player for loop-player

                    # only player
                    else if {_range} is 0:
                        play sound "%{_inst}%" with volume {_volume} with pitch {_pitch} at {_target} for {_target}

                    # range
                    else:
                        if {_target} is an player:
                            set {_center} to location of {_target}
                        
                        else:
                            set {_center} to {_target}
                            
                            set {_x} to x-coordinate of {_target} + ((random integer between -40 and 40) / 100)
                            set {_y} to y-coordinate of {_target} + 0.5
                            set {_z} to z-coordinate of {_target} + ((random integer between -40 and 40) / 100)
                            if "%block at {_target}%" is "note block":

                                # normal
                                execute console command "/particle note %{_x}% %{_y}% %{_z}% 1 0 0 %{_pitch}% 0"

                                # skdragon
                                # music_draw_note({_target}, {_temp::4})

                        loop all players in radius {_range} of {_center}:
                            play sound "%{_inst}%" with volume {_volume} with pitch {_pitch} at {_center} for loop-value-2

            else:
                if {music::ProcessID::*} contains {_id}:
                    set {_target} to {music::playing::ID::%{_id}%}
                    if {_target} is an player:
                        music_end({_target})

                music_stop_from_id({_id})
        
        delete {music::que::%{music::timer}%::*}

# idからmusicを停止する
function music_stop_from_id(id: text):
    set {_target} to {music::playing::ID::%{_id}%}

    remove {_id} from {music::ProcessID::*}
    delete {music::playing::ID::%{_id}%}
    delete {music::playing::IDRev::%{_target}%}
    delete {music::volume::%{_id}%}
    delete {music::range::%{_id}%}
    delete {music::world::%{_id}%}

    delete {music::notes::%{_id}%}
    delete {music::seconds::%{_id}%}
    delete {music::ticks::%{_id}%}
    delete {music::start::%{_id}%}

# キャッシュの削除
function music_clear_cache() :: number:
    set {_cache} to 0
    loop {@maximum_timer} times:
        number of {music::que::%loop-number%::*} is not 0
        add number of {music::que::%loop-number%::*} to {_cache}
        delete {music::que::%loop-number%::*}
    
    delete {music::playing::ID::*}
    delete {music::playing::IDRev::*}
    delete {music::volume::*}
    delete {music::range::*}
    delete {music::world::*}
    delete {music::notes::*}
    delete {music::seconds::*}
    delete {music::ticks::*}
    delete {music::start::*}

    return {_cache}

# playerを中心として音楽を再生
function music_start_play_sound_at_player(p: player, score: string, volume: number, range: number, start: number = 0) :: text:
    if {music::playing::IDRev::%{_p}%} is set:
        set {_id} to {music::playing::IDRev::%{_p}%}
        music_stop_from_id({_id})

    loop 10 times:
        set {_id} to random 6 char string from charset `a-z0-9`
        if {music::ProcessID::*} do not contain {_id}:
            add {_id} to {music::ProcessID::*}
            stop loop

    set {music::playing::ID::%{_id}%} to {_p}
    set {music::playing::IDRev::%{_p}%} to {_id}
    set {music::volume::%{_id}%} to {_volume}
    set {music::range::%{_id}%} to {_range}

    if {_range} is -1:
        set {music::world::%{_id}%} to world of {_p}
    
    music_split_score({_id}, {_score}, {_start})
    return {_id}

# blockを中心として音楽を再生
function music_start_play_sound_at_block(loc: location, score: string, volume: number, range: number, start: number = 0) :: text:
    if {music::playing::IDRev::%{_loc}%} is set:
        set {_id} to {music::playing::IDRev::%{_loc}%}
        music_stop_from_id({_id})

    loop 10 times:
        set {_id} to random 6 char string from charset `a-z0-9`
        if {music::ProcessID::*} do not contain {_id}:
            add {_id} to {music::ProcessID::*}
            stop loop

    set {music::playing::ID::%{_id}%} to {_loc}
    set {music::playing::IDRev::%{_loc}%} to {_id}
    set {music::volume::%{_id}%} to {_volume}
    set {music::range::%{_id}%} to {_range}

    if {_range} is -1:
        set {music::world::%{_id}%} to world of {_loc}

    music_split_score({_id}, {_score}, {_start})
    return {_id}


# 楽譜を分割して読み込む
function music_split_score(id: text, score: text, start: number):

    # 現在時間
    set {_now} to {music::timer} + 5

    # 音楽の再生開始時間
    set {_start.time} to {_now}
    set {music::start::%{_id}%} to {_now}

    # 一時変数
    set {_last.inst} to "*"
    set {_time.add} to ""

    # 統計情報
    set {_time} to 0
    set {_notes} to 0

    # 経過時間
    set {_time.elapsed} to 0

    set {_commands::*} to {_score} split at ""
    loop {_commands::*}:
        loop-value is not ""
        if "!/\?_,^@*()~" contains loop-value:
            set {_last.inst} to loop-value
        
        if "1234567890" contains loop-value:
            set {_time.add} to "%{_time.add}%%loop-value%"

        else:
            set {_time} to {_time.add} parsed as an integer

            # 経過時間と開始時間を比較
            add {_time} to {_time.elapsed}
            if {_start} <= {_time.elapsed}:
                set {_now} to mod({_now} + {_time}, {@maximum_timer})
            
            set {_time} to 0
            set {_time.add} to ""

        if "ABCDEFGHIJKLMNOPQRSTUVWXY" contains loop-value:

            # 音符の追加
            if {_start} <= {_time.elapsed}:
                add "play-%{_id}%-%{_last.inst}%-%loop-value%" to {music::que::%{_now}%::*}
                add 1 to {_notes}
                # broadcast "&e%{_now}%: play-%{_id}%-%{_last.inst}%-%loop-value% &7%{_id}%"

            set {_pitch} to ""
            set {_last.inst} to "*"
    
    set {_now} to mod({_now} + 5, {@maximum_timer})
    add "end-%{_id}%" to {music::que::%{_now}%::*}

    set {music::notes::%{_id}%} to {_notes}

    if {_start.time} < {_now}:
        set {music::ticks::%{_id}%} to {_now} - {_start.time}
    
    else:
        set {music::ticks::%{_id}%} to {@maximum_timer} - {_start.time} + {_now}

    set {music::seconds::%{_id}%} to ({music::ticks::%{_id}%}) / 20


# 現在の再生位置を取得
function music_get_nowposition(id: string) :: number:
    {music::ProcessID::*} contains {_id}
    if {music::start::%{_id}%} < {music::timer}:
        set {_position} to {music::timer} - {music::start::%{_id}%}
    
    else:
        set {_position} to {@maximum_timer} - {music::start::%{_id}%} + {music::timer}
    
    set {_res} to {_position} / {music::ticks::%{_id}%}
    return {_res}

# ピッチを取得
function music_get_pitch(pitch: string) :: number:
    if {_pitch} is "A":
        return 0.5

    if {_pitch} is "B":
        return 0.5297

    if {_pitch} is "C":
        return 0.5612

    if {_pitch} is "D":
        return 0.5946

    if {_pitch} is "E":
        return 0.6299

    if {_pitch} is "F":
        return 0.6674

    if {_pitch} is "G":
        return 0.7071

    if {_pitch} is "H":
        return 0.7491

    if {_pitch} is "I":
        return 0.7937

    if {_pitch} is "J":
        return 0.8408
        
    if {_pitch} is "K":
        return 0.8908

    if {_pitch} is "L":
        return 0.9438

    if {_pitch} is "M":
        return 1.0

    if {_pitch} is "N":
        return 1.0594

    if {_pitch} is "O":
        return 1.1224

    if {_pitch} is "P":
        return 1.1892

    if {_pitch} is "Q":
        return 1.2599

    if {_pitch} is "R":
        return 1.3348

    if {_pitch} is "S":
        return 1.4142

    if {_pitch} is "T":
        return 1.4983

    if {_pitch} is "U":
        return 1.5874

    if {_pitch} is "V":
        return 1.6817

    if {_pitch} is "W":
        return 1.7817

    if {_pitch} is "X":
        return 1.8877

    if {_pitch} is "Y":
        return 2.0


# 楽器を取得
function music_get_instrument(inst: string) :: string:
    if {_inst} is "!":
        return "minecraft:block.note.hat"
    
    if {_inst} is "/":
        return "minecraft:block.note.bell"
    
    if {_inst} is "\":
        return "minecraft:block.note.harp"
    
    if {_inst} is "?":
        return "minecraft:block.note.snare"
    
    if {_inst} is "_":
        return "minecraft:block.note.chime"
    
    if {_inst} is ",":
        return "minecraft:block.note.xylophone"

    if {_inst} is "^":
        return "minecraft:block.note.basedrum"

    if {_inst} is "@":
        return "minecraft:block.note.flute"

    if {_inst} is "*":
        return "minecraft:block.note.pling"

    if {_inst} is "(":
        return "minecraft:block.note.bass"

    if {_inst} is ")":
        return "minecraft:block.note.guitar"
    
    if {_inst} is "~":
        return "minecraft:entity.experience_orb.pickup"


# 音符を表示(現在未使用)
function music_draw_note(loc: location, pitch: string):
    if {_pitch} is "A":
        drawDot count 1, particle note, RGB 119, 215, 0, colorOffset 0.2, 0, 0.2, center {_loc}, randomColor false, visibleRange 20

    if {_pitch} is "B":
        drawDot count 1, particle note, RGB 149, 192, 0, colorOffset 0.2, 0, 0.2, center {_loc}, randomColor false, visibleRange 20

    if {_pitch} is "C":
        drawDot count 1, particle note, RGB 178, 165, 0, colorOffset 0.2, 0, 0.2, center {_loc}, randomColor false, visibleRange 20

    if {_pitch} is "D":
        drawDot count 1, particle note, RGB 204, 134, 0, colorOffset 0.2, 0, 0.2, center {_loc}, randomColor false, visibleRange 20

    if {_pitch} is "E":
        drawDot count 1, particle note, RGB 226, 101, 0, colorOffset 0.2, 0, 0.2, center {_loc}, randomColor false, visibleRange 20

    if {_pitch} is "F":
        drawDot count 1, particle note, RGB 243, 65, 0, colorOffset 0.2, 0, 0.2, center {_loc}, randomColor false, visibleRange 20

    if {_pitch} is "G":
        drawDot count 1, particle note, RGB 252, 30, 0, colorOffset 0.2, 0, 0.2, center {_loc}, randomColor false, visibleRange 20

    if {_pitch} is "H":
        drawDot count 1, particle note, RGB 254, 0, 15, colorOffset 0.2, 0, 0.2, center {_loc}, randomColor false, visibleRange 20

    if {_pitch} is "I":
        drawDot count 1, particle note, RGB 247, 0, 51, colorOffset 0.2, 0, 0.2, center {_loc}, randomColor false, visibleRange 20

    if {_pitch} is "J":
        drawDot count 1, particle note, RGB 232, 0, 90, colorOffset 0.2, 0, 0.2, center {_loc}, randomColor false, visibleRange 20
        
    if {_pitch} is "K":
        drawDot count 1, particle note, RGB 207, 0, 131, colorOffset 0.2, 0, 0.2, center {_loc}, randomColor false, visibleRange 20

    if {_pitch} is "L":
        drawDot count 1, particle note, RGB 174, 0, 169, colorOffset 0.2, 0, 0.2, center {_loc}, randomColor false, visibleRange 20

    if {_pitch} is "M":
        drawDot count 1, particle note, RGB 134, 0, 204, colorOffset 0.2, 0, 0.2, center {_loc}, randomColor false, visibleRange 20

    if {_pitch} is "N":
        drawDot count 1, particle note, RGB 91, 0, 231, colorOffset 0.2, 0, 0.2, center {_loc}, randomColor false, visibleRange 20

    if {_pitch} is "O":
        drawDot count 1, particle note, RGB 45, 0, 249, colorOffset 0.2, 0, 0.2, center {_loc}, randomColor false, visibleRange 20

    if {_pitch} is "P":
        drawDot count 1, particle note, RGB 2, 10, 254, colorOffset 0.2, 0, 0.2, center {_loc}, randomColor false, visibleRange 20

    if {_pitch} is "Q":
        drawDot count 1, particle note, RGB 10, 250, 50, colorOffset 0.2, 0, 0.2, center {_loc}, randomColor false, visibleRange 20

    if {_pitch} is "R":
        drawDot count 1, particle note, RGB 0, 55, 246, colorOffset 0.2, 0, 0.2, center {_loc}, randomColor false, visibleRange 20

    if {_pitch} is "S":
        drawDot count 1, particle note, RGB 0, 104, 224, colorOffset 0.2, 0, 0.2, center {_loc}, randomColor false, visibleRange 20

    if {_pitch} is "T":
        drawDot count 1, particle note, RGB 0, 154, 188, colorOffset 0.2, 0, 0.2, center {_loc}, randomColor false, visibleRange 20

    if {_pitch} is "U":
        drawDot count 1, particle note, RGB 0, 198, 141, colorOffset 0.2, 0, 0.2, center {_loc}, randomColor false, visibleRange 20

    if {_pitch} is "V":
        drawDot count 1, particle note, RGB 0, 233, 88, colorOffset 0.2, 0, 0.2, center {_loc}, randomColor false, visibleRange 20

    if {_pitch} is "W":
        drawDot count 1, particle note, RGB 0, 252, 33, colorOffset 0.2, 0, 0.2, center {_loc}, randomColor false, visibleRange 20

    if {_pitch} is "X":
        drawDot count 1, particle note, RGB 31, 252, 0, colorOffset 0.2, 0, 0.2, center {_loc}, randomColor false, visibleRange 20

    if {_pitch} is "Y":
        drawDot count 1, particle note, RGB 89, 232, 0, colorOffset 0.2, 0, 0.2, center {_loc}, randomColor false, visibleRange 20